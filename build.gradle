plugins {
	id 'java'
	id 'org.springframework.boot' version '3.2.4'
	id "org.asciidoctor.jvm.convert" version "3.3.2"
	id 'io.spring.dependency-management' version '1.1.4'
}

group = 'com.restdocs'
version = '0.0.1-SNAPSHOT'

java {
	sourceCompatibility = '17'
}

ext {
	snippetsDir = file('build/generated-snippets')
}

configurations {
	asciidoctorExt
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jdbc'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation('org.springframework.boot:spring-boot-starter-web') {
		exclude module: 'spring-boot-starter-tomcat'
	}
	implementation('org.springframework.boot:spring-boot-starter-undertow'){
		exclude group: 'io.undertow', module: 'undertow-websockets-jsr'
	}
	compileOnly 'org.projectlombok:lombok'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	runtimeOnly 'mysql:mysql-connector-java:8.0.29'
	annotationProcessor 'org.projectlombok:lombok'
	implementation 'com.google.code.gson:gson'

	implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core'
	implementation group: 'com.fasterxml.jackson.core', name: 'jackson-annotations'
	implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind'
	implementation group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-xml'

	implementation group: 'org.modelmapper', name: 'modelmapper', version: '2.4.2'

	// test
	testImplementation 'org.springframework.boot:spring-boot-starter-test'

	// Spring REST Docs
	asciidoctorExt 'org.springframework.restdocs:spring-restdocs-asciidoctor'
	testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
}

test {
	outputs.dir snippetsDir
	useJUnitPlatform()
}

asciidoctor {
	inputs.dir snippetsDir
	configurations 'asciidoctorExt'
	dependsOn test
}

asciidoctor.doFirst {
	delete file('src/main/resources/static/docs')
}


task copyDocument(type: Copy) {
	dependsOn asciidoctor
	from file("build/docs/asciidoc")
	into file("src/main/resources/static/docs")
}

build {
	dependsOn copyDocument
}

bootJar {
	archiveBaseName = 'restdocs-practice'
	archiveFileName = 'restdocs-practice.jar'
	archiveVersion = '0.0.1'
	dependsOn asciidoctor
	from ("${asciidoctor.outputDir}/html5") {
		into 'static/docs'
	}
}
